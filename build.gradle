plugins {
	id 'maven-publish'
	id 'fabric-loom' version '1.7-SNAPSHOT' apply false

	// https://github.com/ReplayMod/preprocessor
	// https://github.com/Fallen-Breath/preprocessor
	id 'com.replaymod.preprocess' version 'ce1aeb2b'
}

preprocess {
	def mc12001 = createNode('1.20.1', 1_20_01, '')
	def mc12006 = createNode('1.20.6', 1_20_06, '')
	def mc12101 = createNode('1.21.1', 1_21_01, '')

	mc12001.link(mc12006, null)
	mc12006.link(mc12101, null)
}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.tasks.named('build').get()
	}
	doFirst {
		println 'Gathering builds'
		def buildLibs = {
			p -> p.buildDir.toPath().resolve('libs')
		}
		delete fileTree(buildLibs(rootProject)) {
			include '*'
		}
		subprojects {
			copy {
				from(buildLibs(project)) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar', '*-shadow.jar'
				}
				into buildLibs(rootProject)
				duplicatesStrategy DuplicatesStrategy.INCLUDE
			}
		}
	}
}
